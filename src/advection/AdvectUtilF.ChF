c*******************************************************************************
c  SOMAR - Stratified Ocean Model with Adaptive Refinement
c  Developed by Ed Santilli & Alberto Scotti
c  Copyright (C) 2014 University of North Carolina at Chapel Hill
c
c  This library is free software; you can redistribute it and/or
c  modify it under the terms of the GNU Lesser General Public
c  License as published by the Free Software Foundation; either
c  version 2.1 of the License, or (at your option) any later version.
c
c  This library is distributed in the hope that it will be useful,
c  but WITHOUT ANY WARRANTY; without even the implied warranty of
c  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
c  Lesser General Public License for more details.
c
c  You should have received a copy of the GNU Lesser General Public
c  License along with this library; if not, write to the Free Software
c  Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301
c  USA
c
c  For up-to-date contact information, please visit the repository homepage,
c  https://github.com/somarhub.
c*******************************************************************************
#include "CONSTANTS.H"

#define SMALLNUMBER 1.0d-9

      subroutine RiemannSolver (
     &     CHF_FRA[Wgdnv],
     &     CHF_CONST_FRA[WLeft],
     &     CHF_CONST_FRA[WRight],
     &     CHF_CONST_FRA1[advVel],
     &     CHF_CONST_INT[idir],
     &     CHF_BOX[box])

      integer CHF_AUTODECL[i]
      integer n

      if (CHF_NCOMP[WLeft] .eq. 1) then
         CHF_AUTOMULTIDO[box; i]
            if (advVel(CHF_AUTOIX[i]) .gt. SMALLNUMBER) then
                ! Fluid traveling rightward -- Use left state
               Wgdnv(CHF_AUTOIX[i], 0) = WLeft(CHF_AUTOIX[i],0)
            else if (advVel(CHF_AUTOIX[i]) .lt. -SMALLNUMBER) then
                ! Fluid traveling leftward -- Use right state
               Wgdnv(CHF_AUTOIX[i], 0) = WRight(CHF_AUTOIX[i],0)
            else
                ! Fluid is stagnant -- Use average state
               Wgdnv(CHF_AUTOIX[i], 0) = half * (WLeft(CHF_AUTOIX[i],0) + WRight(CHF_AUTOIX[i],0))
            endif
         CHF_ENDDO

      else if (CHF_NCOMP[WLeft] .eq. 2) then
         CHF_AUTOMULTIDO[box; i]
            if (advVel(CHF_AUTOIX[i]) .gt. SMALLNUMBER) then
                ! Fluid traveling rightward -- Use left state
               Wgdnv(CHF_AUTOIX[i], 0) = WLeft(CHF_AUTOIX[i],0)
               Wgdnv(CHF_AUTOIX[i], 1) = WLeft(CHF_AUTOIX[i],1)
            else if (advVel(CHF_AUTOIX[i]) .lt. -SMALLNUMBER) then
                ! Fluid traveling leftward -- Use right state
               Wgdnv(CHF_AUTOIX[i], 0) = WRight(CHF_AUTOIX[i],0)
               Wgdnv(CHF_AUTOIX[i], 1) = WRight(CHF_AUTOIX[i],1)
            else
                ! Fluid is stagnant -- Use average state
               Wgdnv(CHF_AUTOIX[i], 0) = half * (WLeft(CHF_AUTOIX[i],0) + WRight(CHF_AUTOIX[i],0))
               Wgdnv(CHF_AUTOIX[i], 1) = half * (WLeft(CHF_AUTOIX[i],1) + WRight(CHF_AUTOIX[i],1))
            endif
         CHF_ENDDO

      else if (CHF_NCOMP[WLeft] .eq. 3) then
         CHF_AUTOMULTIDO[box; i]
            if (advVel(CHF_AUTOIX[i]) .gt. SMALLNUMBER) then
                ! Fluid traveling rightward -- Use left state
               Wgdnv(CHF_AUTOIX[i], 0) = WLeft(CHF_AUTOIX[i],0)
               Wgdnv(CHF_AUTOIX[i], 1) = WLeft(CHF_AUTOIX[i],1)
               Wgdnv(CHF_AUTOIX[i], 2) = WLeft(CHF_AUTOIX[i],2)
            else if (advVel(CHF_AUTOIX[i]) .lt. -SMALLNUMBER) then
                ! Fluid traveling leftward -- Use right state
               Wgdnv(CHF_AUTOIX[i], 0) = WRight(CHF_AUTOIX[i],0)
               Wgdnv(CHF_AUTOIX[i], 1) = WRight(CHF_AUTOIX[i],1)
               Wgdnv(CHF_AUTOIX[i], 2) = WRight(CHF_AUTOIX[i],2)
            else
                ! Fluid is stagnant -- Use average state
               Wgdnv(CHF_AUTOIX[i], 0) = half * (WLeft(CHF_AUTOIX[i],0) + WRight(CHF_AUTOIX[i],0))
               Wgdnv(CHF_AUTOIX[i], 1) = half * (WLeft(CHF_AUTOIX[i],1) + WRight(CHF_AUTOIX[i],1))
               Wgdnv(CHF_AUTOIX[i], 2) = half * (WLeft(CHF_AUTOIX[i],2) + WRight(CHF_AUTOIX[i],2))
            endif
         CHF_ENDDO

      else
          do n = 0, CHF_NCOMP[WLeft]-1
             CHF_AUTOMULTIDO[box; i]
                if (advVel(CHF_AUTOIX[i]) .gt. SMALLNUMBER) then
                    ! Fluid traveling rightward -- Use left state
                   Wgdnv(CHF_AUTOIX[i], n) = WLeft(CHF_AUTOIX[i],n)
                else if (advVel(CHF_AUTOIX[i]) .lt. -SMALLNUMBER) then
                    ! Fluid traveling leftward -- Use right state
                   Wgdnv(CHF_AUTOIX[i], n) = WRight(CHF_AUTOIX[i],n)
                else
                    ! Fluid is stagnant -- Use average state
                   Wgdnv(CHF_AUTOIX[i], n) = half * (WLeft(CHF_AUTOIX[i],n) + WRight(CHF_AUTOIX[i],n))
                endif
             CHF_ENDDO
          end do
      endif

      return
      end


      subroutine AvgStates (
     &     CHF_FRA[Wgdnv],
     &     CHF_CONST_FRA[WLeft],
     &     CHF_CONST_FRA[WRight],
     &     CHF_CONST_INT[idir],
     &     CHF_BOX[box])

      integer CHF_AUTODECL[i]
      integer n
      real_t sl,sr

      do n = 0, CHF_NCOMP[WLeft]-1
         CHF_AUTOMULTIDO[box; i]
            sl =  WLeft(CHF_AUTOIX[i],n)
            sr = WRight(CHF_AUTOIX[i],n)
            Wgdnv(CHF_AUTOIX[i], n) = half*(sl+sr)
         CHF_ENDDO
      end do

      return
      end


      subroutine QuasilinearUpdateF (
     &    CHF_FRA[AdWdx],
     &    CHF_CONST_FRA[WHalf],
     &    CHF_CONST_FRA1[cellVel],
     &    CHF_CONST_REAL[scale],
     &    CHF_CONST_INT[idir],
     &    CHF_BOX[box])

      integer CHF_DDECL[i;j;k]
      integer CHF_DDECL[ii;jj;kk]
      integer n

      CHF_DTERM[
      ii = CHF_ID(idir,0);
      jj = CHF_ID(idir,1);
      kk = CHF_ID(idir,2)]

      do n=0, CHF_NCOMP[AdWdx]-1
        CHF_MULTIDO[box;i;j;k]
          AdWdx(CHF_IX[i;j;k],n) = scale * cellVel(CHF_IX[i;j;k]) *
     &        (WHalf(CHF_IX[i+ii;j+jj;k+kk],n) - WHalf(CHF_IX[i;j;k],n))
        CHF_ENDDO
      end do

      return
      end
